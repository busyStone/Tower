import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
// This does not break the build when Android Studio is missing the JRebel for Android plugin.
apply plugin: 'com.zeroturnaround.jrebel.android'
//apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'

apply plugin: 'bugly'

dependencies {
    compile 'com.google.android.gms:play-services-maps:7.8.0'
    compile 'com.google.android.gms:play-services-location:7.8.0'
//    compile 'com.google.android.gms:play-services-analytics:7.8.0'

    compile 'com.sothree.slidinguppanel:library:2.0.2'

    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'

    //DroneKit-Android client library.
//    compile 'com.o3dr.android:dronekit-android:2.6.6'
    compile(name: 'dronekit-android.2.6.6', ext: 'aar')

    compile 'me.grantland:autofittextview:0.2.1'
    compile(name:'shimmer-android-release', ext:'aar')

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'

    compile 'com.jakewharton.timber:timber:3.1.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
//    compile('com.crashlytics.sdk.android:crashlytics:2.5.0@aar') {
//        transitive = true;
//    }

    //Charts and graph library
    compile 'com.github.lecho:hellocharts-library:1.5.5@aar'

    compile 'com.tencent.bugly:crashreport:1.2.9'
}

bugly {
    appId COM_SKYBORNE_GCS_ANDROID_BUGLY_APPID
    appKey COM_SKYBORNE_GCS_ANDROID_BUGLY_APPKEY
}

//Decomposed version name and code (https://plus.google.com/+JakeWharton/posts/6f5TcVPRZij)
def versionMajor = 3
def versionMinor = 2
def versionPatch = 0
def versionBuild = 14 //bump for dogfood builds, public betas, etc.

//Logging levels
def logLevelVerbose = 2;
def logLevelDebug = 3;
def logLevelInfo = 4;
def logLevelWarn = 5;
def logLevelError = 6;
def logLevelAssert = 7;

// version code 供程序用来区别版本号  即 是否需要升级
// version name 供人看
//
// 为了 内部测试时 也能支持自动升级  保留 versionBuild
// 为 versionName 增加 date 后缀

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    def appTitle = "Tower"

    defaultConfig {
        applicationId 'com.skyborne.gcs.android'
        minSdkVersion 14
        targetSdkVersion 22

        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${appTitle}-v${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "boolean", "WRITE_LOG_FILE", "true"
        buildConfigField "int", "LOG_FILE_LEVEL", "$logLevelDebug"
        buildConfigField "boolean", "ENABLE_CRASHLYTICS", "false"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    //FIXME: remove this after lint errors have been taken care of
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            try {
                storeFile file("../../skyborne.keystore")
                storePassword COM_SKYBORNE_GCS_ANDROID_KEYSTORE_PWD
                keyAlias "skyborne"
                keyPassword COM_SKYBORNE_GCS_ANDROID_KEY_PWD
            }
            catch (e) {
                throw new InvalidUserDataException(
                        "You should define COM_SKYBORNE_GCS_ANDROID_KEYSTORE_PWD and COM_SKYBORNE_GCS_ANDROID_KEY_PWD in gradle.properties.")
            }
        }
    }

    def versionBuildTime=getDate()

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def buildType = variant.buildType.getName()
                def versionCode = defaultConfig.versionCode
                output.outputFile = new File(file.parent, "${appTitle}-${buildType}.${versionCode}.apk")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
            resValue "string", "app_title", "${appTitle} Dev"
            manifestPlaceholders = [AMAP_API_V2_KEY: "e08b6c1fc13e010e5fcc0abd2b66f2f0"]
        }

        beta {
            signingConfig signingConfigs.release
            applicationIdSuffix ".beta"
            versionNameSuffix ".beta.${versionBuild}"
            resValue "string", "app_title", "${appTitle} Beta"
            manifestPlaceholders = [AMAP_API_V2_KEY: "6ca4669e0070d2d2bfcec4c419eec2d8"]
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "true"
        }

        release {
            signingConfig signingConfigs.release
            resValue "string", "app_title", "${appTitle}"
            buildConfigField "boolean", "WRITE_LOG_FILE", "false"
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "true"
        }
    }
}

/**
 * @return The most recent git tag to be used as version name for the app.
 */
def getGitVersion(){
    def cmd = "git describe --tag"
    try {
        def proc = cmd.execute()
        return proc.text.trim()
    }catch(IOException e){
        //Unable to find 'git'
        return "please update version name manually"
    }
}

def getDate() {
    def df = new SimpleDateFormat("yyyyMMddHHmmss") //you can change it
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}
